add_library(imgui STATIC)
target_sources(imgui PRIVATE
    imgui.cpp imgui.h
    imgui_demo.cpp
    imgui_draw.cpp
    imgui_widgets.cpp
    imgui_tables.cpp
    backends/imgui_impl_sdl.cpp backends/imgui_impl_sdl.h
    backends/imgui_impl_opengl3.cpp backends/imgui_impl_opengl3.h
)

include(GNUInstallDirs)

target_include_directories(
        imgui PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}> $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/backends>
        $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>)
target_compile_definitions(
        imgui
        PUBLIC
        IMGUI_DISABLE_INCLUDE_IMCONFIG_H
        $<$<BOOL:${IMGUI_DISABLE_OBSOLETE_FUNCTIONS}>:IMGUI_DISABLE_OBSOLETE_FUNCTIONS>
)
#target_compile_features(${PROJECT_NAME} PRIVATE cxx_std_11)
set_target_properties(imgui PROPERTIES DEBUG_POSTFIX "d")

install(
        TARGETS imgui
        EXPORT imgui-config
        ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR})
install(
        EXPORT imgui-config
        NAMESPACE imgui::
        DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/imgui)
install(FILES imgui.h DESTINATION ${CMAKE_INSTALL_INCLUDEDIR})